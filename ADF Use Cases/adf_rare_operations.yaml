id: dfaab05d-bc3a-4d22-b8fd-696400ff2c97
name: Rare Azure Data Factory Operations
description: Identifies rare Azure Data Factory operations.
severity: Medium
requiredDataConnectors:  
  - connectorId: AzureActivity
    dataTypes:
      - AzureActivity
queryFrequency: 1d
queryPeriod: 1d
triggerOperator: gt
triggerThreshold: 1
tactics:
  - Credential Access
  - Persistence
query:
  let starttime = 7d;
  let endtime = 1d;
  // The number of operations below which an IP address is considered an unusual source of role assignment operations
  let alertOperationThreshold = 3;
  let SensitiveOperationList = dynamic(
  ["Create or Update any Data Factory.", "Delete Data Factory", "Get GitHub access token", "Get DataPlane access", "Configure Factory Repository"]);
  let SensitiveActivity = AzureActivity
  | where OperationName in~ (SensitiveOperationList)
  | where ActivityStatus =~ "Succeeded";
  SensitiveActivity
  | where TimeGenerated between (ago(starttime) .. ago(endtime))
  | summarize count() by CallerIpAddress, Caller
  | where count_ >= alertOperationThreshold
  | join kind = rightanti ( 
  SensitiveActivity
  | where TimeGenerated >= ago(endtime)
  | summarize StartTimeUtc = min(TimeGenerated), EndTimeUtc = max(TimeGenerated), ActivityTimeStamp = makelist(TimeGenerated), ActivityStatus = makelist(ActivityStatus), 
  OperationIds = makelist(OperationId), CorrelationIds = makelist(CorrelationId), Resources = makelist(Resource), ResourceGroups = makelist(ResourceGroup), ResourceIds = makelist(ResourceId), ActivityCountByCallerIPAddress = count()  
  by CallerIpAddress, Caller, OperationName
  ) on CallerIpAddress, Caller
  | extend timestamp = StartTimeUtc, AccountCustomEntity = Caller, IPCustomEntity = CallerIpAddress
